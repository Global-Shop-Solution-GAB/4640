Program.Sub.ScreenSU.Start
gui.F_Contacts..create
gui.F_Contacts..caption("Form1")
gui.F_Contacts..size(5970,3180)
gui.F_Contacts..position(0,0)
gui.F_Contacts..event(unload,f_contacts_unload)
gui.F_Contacts..alwaysontop(False)
gui.F_Contacts..fontname("Arial")
gui.F_Contacts..fontsize(8)
gui.F_Contacts..forecolor(0)
gui.F_Contacts..fontstyle(,,,,)
gui.F_Contacts..BackColor(-2147483633)
gui.F_Contacts..controlbox(True)
gui.F_Contacts..maxbutton(False)
gui.F_Contacts..minbutton(True)
gui.F_Contacts..mousepointer(0)
gui.F_Contacts..moveable(True)
gui.F_Contacts..sizeable(False)
gui.F_Contacts..ShowInTaskBar(True)
gui.F_Contacts..titlebar(True)
gui.F_Contacts.lbl1.create(label,"",,0,0,0,0,0,True,0,,8,0,0)
gui.F_Contacts.lbl1.defaultvalue("")
gui.F_Contacts.lbl1.controlgroup(0)
gui.F_Contacts.txtContacts.create(textboxm)
gui.F_Contacts.txtContacts.text("")
gui.F_Contacts.txtContacts.visible(True)
gui.F_Contacts.txtContacts.size(5550,1500)
gui.F_Contacts.txtContacts.zorder(0)
gui.F_Contacts.txtContacts.position(200,700)
gui.F_Contacts.txtContacts.enabled(True)
gui.F_Contacts.txtContacts.alignment(0)
gui.F_Contacts.txtContacts.fontname("Arial")
gui.F_Contacts.txtContacts.fontsize(8)
gui.F_Contacts.txtContacts.BackColor(-2147483643)
gui.F_Contacts.txtContacts.defaultvalue("")
gui.F_Contacts.txtContacts.controlgroup(0)
gui.F_Contacts.cmdSave.create(button)
gui.F_Contacts.cmdSave.caption("Save")
gui.F_Contacts.cmdSave.visible(True)
gui.F_Contacts.cmdSave.size(855,375)
gui.F_Contacts.cmdSave.zorder(0)
gui.F_Contacts.cmdSave.position(200,2300)
gui.F_Contacts.cmdSave.enabled(True)
gui.F_Contacts.cmdSave.fontname("Arial")
gui.F_Contacts.cmdSave.fontsize(8)
gui.F_Contacts.cmdSave.event(click,cmdsave_click)
gui.F_Contacts.cmdSave.defaultvalue("")
gui.F_Contacts.cmdSave.controlgroup(0)


Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Variable.Global.sCustomer.Declare(String)
Variable.Global.sInvoiceNo.Declare(String)
Variable.Global.sOrderNo.Declare(String)
Variable.Global.sCustPO.Declare(String)
Variable.Global.sCompany.Declare(String)
Variable.Global.sCustomerName.Declare(String)
Variable.Global.iBIRUNID.Declare(Long,0)
Variable.Global.iLOGID.Declare(Long,0)
Variable.Global.bParamCheck.Declare(Boolean,False)
Program.Sub.Preflight.End

Program.Sub.Main.Start
'Coded by: SMC
'Project Start Date: 11/28/2010
'Hooks:
'  ARS002A1-POPULATE-HOOK (MAIN CUSTOMER MAINT SCREEN) (000014350)
'  ARS002A1-SCRIPT-2-HOOK (MAIN CUSTOMER MAINT SCREEN) (000014370)
'  Invoice Batch Job Stream Post Save Process Hook (000015225)

'5/19/11 - SMC - Made a change in ProcessINV.  Made sure the e-Invoicing is searching specifically for an "e-Invoice" report with an INVOICE parameter.

'8/24/2012 - CK5 - Added date deferment to send emails after consolidation.
'Courier will send Invoice emails after consolidation(Defered date set to blank) or @11:59 PM (latest) on the day they were processed.

'trt mod
'adding in print report from preprpoc

'F.Intrinsic.Debug.InvokeDebugger
'F.Intrinsic.Debug.Stop
'F.Intrinsic.Debug.Stop
F.Intrinsic.Control.If(V.Caller.Hook,=,14310)
	V.Passed.000202.Set("e-Invoice Cont")
	F.Intrinsic.Control.End

F.Intrinsic.Control.EndIf

F.ODBC.Connection!conx.OpenConnection(V.Ambient.PDSN,V.Ambient.PUser,V.Ambient.PPass)

F.Intrinsic.Control.If(V.Caller.Hook,=,14330)
	F.Intrinsic.Control.CallSub(Contacts)
F.Intrinsic.Control.ElseIf(V.Caller.Hook,=,15225)
	
	F.Intrinsic.Control.If(V.Passed.009006,=,"Y")
		V.Global.sCustomer.Set(V.Passed.009001)
		V.global.sInvoiceNo.Set(V.Passed.009000)
		'read our global passed variabl since that holds the value, if false we know we need to check for report params that are required.
		'else if it's true we know we don't have to check and can just do it withouth the param check.
		F.Intrinsic.Variable.PassedExists("GLOBAL",V.Global.bParamCheck)
		F.Intrinsic.Control.If(V.Global.bParamCheck,=,True)
			F.Intrinsic.Control.If(V.Passed.Global.Trim,=,"True")
				V.Global.bParamCheck.Set(True)
			F.Intrinsic.Control.Else
				V.Global.bParamCheck.Set(False)
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.Else
			'set it back to false, the global passed variable doesn't exist within our hook for some reason.
			V.Global.bParamCheck.Set(False)
		F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.CallSub(Einvoicing)
	F.Intrinsic.Control.Else
		F.Intrinsic.Control.CallSub(Unload)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.Elseif(V.Caller.Hook,=,1)
	V.Global.sCustomer.Set("011427")
	V.Global.sInvoiceNo.Set("000049")
	F.Intrinsic.Control.CallSub(Einvoicing)
	F.Intrinsic.Control.End

F.Intrinsic.Control.EndIf

Program.Sub.Main.End

Program.Sub.Contacts.Start
F.Intrinsic.Control.SetErrorHandler("Contacts_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sQuery.Declare(String)
V.Local.sEmails.Declare(String)

F.Intrinsic.String.Concat("SELECT * FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='",V.Passed.000002,"'",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRO("rstContacts",V.Local.sQuery)
F.Intrinsic.Control.DoUntil(V.ODBC.conx!rstContacts.EOF,=,True)
	F.Intrinsic.Control.If(V.Local.sEmails,=,"")
		V.Local.sEmails.Set(V.ODBC.conx!rstContacts.FieldValTrim!CUST_EMAIL)
	F.Intrinsic.Control.Else
		F.Intrinsic.String.Concat(V.Local.sEmails,", ",V.ODBC.conx!rstContacts.FieldValTrim!CUST_EMAIL,V.Local.sEmails)
	F.Intrinsic.Control.EndIf

	F.ODBC.conx!rstContacts.MoveNext
F.Intrinsic.Control.Loop
F.ODBC.conx!rstContacts.Close

Gui.F_Contacts.txtContacts.Text(V.Local.sEmails)

F.Intrinsic.String.Concat("e-Invoicing contacts for ",V.Passed.000005,V.Local.sQuery)
Gui.F_Contacts..Caption(V.Local.sQuery)

Gui.F_Contacts..Show

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Contacts_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_4640_Invoice_Delivery_VAR1.g2u",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.Debug.SetLA(V.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.Contacts.End

Program.Sub.eInvoicing.Start
F.Intrinsic.Control.SetErrorHandler("eInvoicing_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sQuery.Declare(String)
V.Local.sContacts.Declare(String)
V.Local.sContacts.Redim(-1,-1)
V.Local.sDelete.Declare(String)
V.Local.sDelete.Redim(-1,-1)
V.Local.iUB.Declare(Long,-1)
V.Local.bExists.Declare(Boolean)
V.Local.iC.Declare(Long)
V.Local.iEmailSalesRep.Declare(Long,-1)
V.Local.sEmail.Declare(String)
V.local.sTemp.Declare(String)
V.Local.sUserName.Declare(String)
V.Local.sUserEmail.Declare(String)
V.Local.iRecord.Declare(Long,-1)
V.Local.sInvoicePath.Declare(String)
V.Local.sBody.Declare(String)
V.Local.sSubject.Declare(String)
V.Local.sFile.Declare(String)
V.Local.bFileExists.Declare(Boolean)
V.Local.iType.Declare(Long)
V.Local.iCourier.Declare(Long)
V.Local.iID.Declare(Long)
V.Local.sAttach.Declare(String)
V.Local.sParam.Declare(String)
V.Local.sDate.Declare(String)
V.Local.sSender.Declare(String)
V.Local.sRecip.Declare(String)
V.local.dNextDate.Declare(Date)
V.Local.sPDFName.Declare(String)
v.Local.sCRLF.Declare(String)

F.Intrinsic.Date.DateAdd("D",1,V.Ambient.Date,V.Local.dNextDate)

F.Intrinsic.Debug.Stop

F.Intrinsic.Debug.SetLA("Emailing Invoice No ",V.Global.sInvoiceNo," to customer ",V.Global.sCustomer)
F.Intrinsic.Control.CallSub(Check_econtacts_meth)
V.Local.iType.Set(V.Args.iType)

F.Global.Security.getuserID(V.Caller.User,V.Caller.CompanyCode,V.Local.iID)
'Gets all Customer Contacts and builds into sContacts array
F.Intrinsic.Control.If(V.Local.iType,=,0)
	F.Intrinsic.String.Concat("SELECT EMAIL1, NAME FROM CONTACT WHERE TYPE='C' AND CUST='",V.Global.sCustomer,"'",V.Local.sQuery)
	F.ODBC.Connection!conx.OpenRecordsetRO("rstCust",V.Local.sQuery)
	F.Intrinsic.Control.DoUntil(V.ODBC.conx!rstCust.EOF,=,True)
		F.Intrinsic.Math.Add(V.Local.iUB,1,V.Local.iUB)
		F.Intrinsic.Control.If(V.Local.iUB,=,0)
			V.Local.sContacts.Redim(0,0)
		F.Intrinsic.Control.Else
			V.Local.sContacts.RedimPreserve(0,V.Local.iUB)
		F.Intrinsic.Control.EndIf
		'mod by trt, switch this, whend doing consolidation it is mixing up the email with the name and the name into the email field in courier body
		F.Intrinsic.String.Concat(V.ODBC.conx!rstCust.FieldValTrim!EMAIL1,"*!*",V.ODBC.conx!rstCust.FieldValTrim!NAME,V.Local.stemp)
'		F.Intrinsic.String.Concat(V.ODBC.conx!rstCust.FieldValTrim!Name,"*!*",V.ODBC.conx!rstCust.FieldValTrim!Email1,V.Local.sTemp)
		V.Local.sContacts(v.Local.iUB).Set(V.Local.sTemp)
		F.ODBC.conx!rstCust.MoveNext
	F.Intrinsic.Control.Loop
	F.ODBC.conx!rstCust.Close
F.Intrinsic.Control.EndIf

V.Local.iUB.Set(-1)
F.Intrinsic.String.Concat("SELECT * FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='",V.Global.sCustomer,"'",V.Local.sQuery)
F.ODBC.connection!conx.OpenRecordsetRW("rstCust",V.Local.sQuery)
F.Intrinsic.Control.DoUntil(V.ODBC.conx!rstCust.EOF,=,True)
	'If this is the first loop, iEmailSalesRep=-1.  After that, iEmailSalesRep will be set as either 0 or 1, depending on the option set for the customer.  So in the first loop, where iEmailSalesRep=1, is where we process the invoice, so it only happens once.
	F.Intrinsic.Control.If(V.Local.iEmailSalesRep,=,-1)
		'Generates Invoice PDF
		F.Intrinsic.String.Concat("SELECT ORDER_NO FROM ORDER_HIST_HEAD WHERE INVOICE='",V.Global.sInvoiceNo,"'",V.Local.sQuery)
		F.ODBC.Connection!conx.OpenRecordsetRO("rstOrder",V.Local.sQuery)
		F.Intrinsic.Control.If(V.ODBC.conx!rstOrder.EOF,<>,True)
			V.Global.sOrderNo.Set(V.ODBC.conx!rstOrder.FieldValTrim!ORDER_NO)
			'Gets e-Invoicing Body Text
			F.Intrinsic.Control.CallSub(Popdefaulttext)
			V.Local.sBody.Set(V.Args.Body)
			V.Local.sSubject.Set(V.Args.Subject)
			F.Intrinsic.Control.CallSub("ProcessInv")
			V.Local.sInvoicePath.Set(V.Args.sAttach)
		F.Intrinsic.Control.Else
			F.Intrinsic.UI.Msgbox("Unable to find order number. Please contact Global Shop","e-Invoicing")
			F.Intrinsic.Control.CallSub(Unload)
		F.Intrinsic.Control.EndIf
		F.ODBC.conx!rstOrder.Close

		F.Intrinsic.Control.If(V.ODBC.conx!rstCust.fieldvaltrim!SALES_EMAIL,=,True)
			V.Local.iEmailSalesRep.Set(1)
			F.Intrinsic.String.Concat("SELECT S.SALESPERSON AS EMAIL, N.SALESPERSON FROM SALESPEOPLE S, CUSTOMER_MASTER C, V_SALESPEOPLE N WHERE N.SALESPERSON_CODE=S.SALESPERSON_CODE AND C.SALESPERSON=S.SALESPERSON_CODE AND C.CUSTOMER='",V.Global.sCustomer,"' AND S.KEY1='AR2' AND S.KEY2='SLM'",V.Local.sQuery)
			F.ODBC.Connection!conx.OpenRecordsetRO("rstSalesperson",V.Local.sQuery)
			F.Intrinsic.Control.If(V.ODBC.conx!rstSalesperson.EOF,<>,True)
				V.Local.sTemp.Redim(0,1)
				V.Local.sTemp(0).Set(V.ODBC.conx!rstSalesperson.FieldValTrim!SALESPERSON)
				V.Local.sTemp(1).Set(V.ODBC.conx!rstSalesperson.FieldValTrim!EMAIL)
				'email
				F.Global.Security.GetFullName(V.Caller.User,V.Local.sUserName)
				F.Global.Security.GetUserEmail(V.Caller.User,V.Local.sUserEmail)
				F.Intrinsic.Control.If(V.Local.sUserEmail,=,"")
					V.Local.sUserName.Set(V.Local.sTemp(1))
					V.Local.sUserEmail.Set(V.Local.sTemp(0))
				F.Intrinsic.Control.EndIf
'				F.Global.Messaging.CreateEMMessage(V.ODBC.conx!rstSalesperson.FieldValTrim!EMAIL,V.ODBC.conx!rstSalesperson.FieldValTrim!SALESPERSON,V.ODBC.conx!rstSalesperson.FieldValTrim!EMAIL,V.ODBC.conx!rstSalesperson.FieldValTrim!SALESPERSON,V.Local.sSubject,V.Local.sBody)
				F.Intrinsic.Debug.SetLA("Emailing Sales Rep: ",V.ODBC.conx!rstSalesperson.FieldValTrim!SALESPERSON," email: ",V.ODBC.conx!rstSalesperson.FieldValTrim!EMAIL)
				F.Intrinsic.Control.If(V.ODBC.conx!rstSalesperson.FieldValTrim!EMAIL,<>,"")
'					F.Global.Messaging.CreateEMMessage(V.ODBC.conx!rstSalesperson.FieldValTrim!EMAIL,V.ODBC.conx!rstSalesperson.FieldValTrim!SALESPERSON,V.ODBC.conx!rstSalesperson.FieldValTrim!EMAIL,V.ODBC.conx!rstSalesperson.FieldValTrim!SALESPERSON,V.Local.sSubject,V.Local.sBody,V.Local.sInvoicePath,False)
					'use Q Message
					'build recipeient  Name*!*Email and sender since its them sending them it's going to be the same.
					F.Intrinsic.String.Build("{0}*!*{1}",V.ODBC.conx!rstSalesperson.FieldValTrim!Salesperson,V.ODBC.conx!rstSalesperson.FieldValTrim!Email,V.Local.sRecip)
					F.Intrinsic.String.Build("{0}*!*{1}",V.Local.sUserEmail,V.Local.sUserName,V.Local.sSender)
					F.Intrinsic.String.Concat(V.Global.sInvoiceNo,"-",V.Global.sOrderNo,".pdf*!*",V.Caller.PluginsDir,"\Invoices\",V.Local.sAttach)
					F.Intrinsic.String.Concat(V.Global.sCustomer,"*!*15*!*0",V.Local.sParam)
					F.Intrinsic.string.Concat(V.local.dNextDate," 23:59:00",V.Local.sDate)
					F.Intrinsic.String.Build("{0}-{1}.pdf",V.Global.sInvoiceNo,V.Global.sOrderNo,V.Local.sPDFName)
'					F.Global.Messaging.QueueMessage(V.Caller.CompanyCode,V.Local.iID,"",V.Local.sSubject,V.Local.sSender,V.Local.sRecip,V.Local.sBody,5,,False,,,,,,,,V.Local.sAttach,False)
					Function.Global.Messaging.QueueMessage(V.Caller.CompanyCode,V.local.iID,"",V.Local.sSubject,V.Local.sSender,V.Local.sRecip,V.local.sBody,5,,False,,V.Local.sDate,V.local.sParam,v.Local.sPDFName,,,,V.Local.sAttach,False)
					'Replace LF by CRLF on the file to avoid email server issues with LF character 
'					F.Intrinsic.String.Concat(v.ASCII.13,v.ASCII.10,v.Local.sCRLF)'CRLF
'					F.Intrinsic.String.Replace(v.Local.sBody,v.ASCII.10,v.Local.sCRLF,v.Local.sBody)
'					Function.Global.Messaging.QueueMessage(V.Caller.CompanyCode,V.local.iID,"",V.Local.sSubject,V.Local.sSender,V.Local.sRecip,,5,,False,,,V.local.sParam,v.Local.sPDFName,,,,V.Local.sAttach,False,V.local.sBody)
				F.Intrinsic.Control.EndIf
			F.Intrinsic.Control.EndIf
			F.ODBC.conx!rstSalesperson.Close
		F.Intrinsic.Control.Else
			V.Local.iEmailSalesRep.Set(0)
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.EndIf

	'Checks to see if the contact e-mail written in ATG_CUSTOMER_EMAIL is still a contact e-mail for the customer in CRM.  Otherwise, it will not send an e-mail out to that address, and address to the "Delete" array, which is purged from ATG_CUSTOMER_EMAIL at the end of the subroutine
	F.intrinsic.control.if(V.local.iType,=,0)
		V.Local.bExists.Set(False)
		F.Intrinsic.Control.For(V.Local.iC,0,V.Local.sContacts.UBound,1)
			F.Intrinsic.String.Split(V.Local.sContacts(v.Local.iC).Trim,"*!*",V.Local.sTemp)
			F.Intrinsic.Control.If(V.ODBC.conx!rstCust.FieldValTrim!CUST_EMAIL,=,V.Local.sTemp(0).Trim)
				V.Local.bExists.Set(True)
				F.Intrinsic.Control.ExitFor(V.Local.iC)
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.Next(V.Local.iC)
	F.Intrinsic.Control.Else
		V.Local.bExists.Set(True)
		V.Local.sTemp.Redim(0,1)
		V.Local.sTemp(0).Set(V.ODBC.conx!rstCust.FieldValTrim!CUST_EMAIL)
		V.Local.sTemp(1).Set(V.ODBC.conx!rstCust.FieldValTrim!CUST_EMAIL)
	F.Intrinsic.Control.EndIf

'F.Intrinsic.Debug.InvokeDebugger
'F.Intrinsic.Debug.Stop

	F.Intrinsic.Control.If(V.Local.bExists,=,True)
		'email
		F.Global.Security.GetFullName(V.Caller.User,V.Local.sUserName)
		F.Global.Security.GetUserEmail(V.Caller.User,V.Local.sUserEmail)
		F.Intrinsic.Control.If(V.Local.sUserEmail,=,"")
			V.Local.sUserName.Set(V.Local.sTemp(1))
			V.Local.sUserEmail.Set(V.Local.sTemp(0))
		F.Intrinsic.Control.EndIf
'		F.Global.Messaging.CreateEMMessage(V.Local.sTemp(0),V.Local.sTemp(1),V.Local.sUserEmail,V.Local.sUserName,V.Local.sSubject,V.Local.sBody)
		F.Global.Messaging.IsCourierRunning(V.Local.iCourier)
		F.Intrinsic.Control.If(V.Local.iCourier,=,1)
			F.Global.Messaging.CreateEMMessage(V.Local.sTemp(0),V.Local.sTemp(1),V.Local.sUserEmail,V.Local.sUserName,V.Local.sSubject,V.Local.sBody,V.Local.sInvoicePath,False)
		F.Intrinsic.Control.Else
			'sender string Email*!*Name
			F.Intrinsic.String.Concat(V.Local.sUserEmail,"*!*",V.Local.sUserName,V.Local.sSender)
			'recip string Name*!*Email
			F.Intrinsic.String.Concat(V.Local.sTemp(1),"*!*",V.Local.sTemp(0),V.local.sRecip)
			F.Intrinsic.String.Concat(V.Global.sInvoiceNo,"-",V.Global.sOrderNo,".pdf*!*",V.Caller.PluginsDir,"\Invoices\",V.Local.sAttach)
			F.Intrinsic.String.Concat(V.Global.sCustomer,"*!*15*!*0",V.Local.sParam)
			'Meta2 data
			F.Intrinsic.String.Build("{0}-{1}.pdf",V.Global.sInvoiceNo,V.Global.sOrderNo,V.Local.sPDFName)
'			F.Global.Security.getuserID(V.Caller.User,V.Caller.CompanyCode,V.Local.iID)
			F.Intrinsic.string.Concat(V.local.dNextDate," 23:59:00",V.Local.sDate)
			'Replace LF by CRLF on the file to avoid email server issues with LF character 
			F.Intrinsic.String.Concat(v.ASCII.13,v.ASCII.10,v.Local.sCRLF)'CRLF
			F.Intrinsic.String.Replace(v.Local.sBody,v.ASCII.10,v.Local.sCRLF,v.Local.sBody)
			Function.Global.Messaging.QueueMessage(V.Caller.CompanyCode,V.local.iID,"",V.Local.sSubject,V.Local.sSender,V.Local.sRecip,V.local.sBody,5,,False,,V.Local.sDate,V.local.sParam,v.Local.sPDFName,,,,V.Local.sAttach,False)
	F.Intrinsic.Control.EndIf
		'write record
		F.Intrinsic.Control.If(V.Local.iRecord,=,-1)
			F.ODBC.Connection!conx.OpenRecordsetRW("rstRecord","SELECT * FROM ATG_CUST_EMAIL_HIST")
			
			F.ODBC.conx!rstRecord.AddNew
			F.ODBC.conx!rstRecord.Set!CUSTOMER(V.Global.sCustomer)
			F.ODBC.conx!rstRecord.Set!INVOICE_NO(V.global.sInvoiceNo)
			F.ODBC.conx!rstRecord.Set!EMAIL_DATE(V.Ambient.Now)

			F.ODBC.conx!rstRecord.Update
			F.ODBC.conx!rstRecord.Close
			V.Local.iRecord.Set(0)
		F.Intrinsic.Control.Endif
	F.Intrinsic.Control.Else
		F.Intrinsic.Math.Add(V.Local.iUB,1,V.Local.iUB)
		F.Intrinsic.Control.If(V.Local.iUB,=,0)
			V.Local.sDelete.Redim(0,0)
		F.Intrinsic.Control.Else
			V.Local.sDelete.RedimPreserve(0,V.Local.iUB)
		F.Intrinsic.Control.EndIf
		V.Local.sDelete(v.Local.iUB).Set(V.ODBC.conx!rstCust.FieldValTrim!CUST_EMAIL)
	F.Intrinsic.Control.EndIf
	F.ODBC.conx!rstCust.MoveNext
F.Intrinsic.Control.Loop
F.ODBC.conx!rstCust.Close
	
F.Intrinsic.Control.For(V.Local.iC,0,V.Local.sDelete.UBound,1)
	F.Intrinsic.String.Concat("DELETE FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='",V.Global.sCustomer,"' AND CUST_EMAIL='",V.Local.sDelete(v.Local.iC),"'",V.Local.sQuery)
	F.ODBC.connection!conx.Execute(V.Local.sQuery)
F.Intrinsic.Control.Next(V.Local.iC)

F.Intrinsic.Control.CallSub("Unload")



F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("eInvoicing_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_4640_Invoice_Delivery_VAR1.g2u",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.Debug.SetLA(V.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.eInvoicing.End

program.sub.f_contacts_unload.start
F.Intrinsic.Control.CallSub(Unload)

program.sub.f_contacts_unload.End

program.sub.cmdsave_click.start
F.Intrinsic.Control.SetErrorHandler("cmdsave_click_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sQuery.Declare(String)
V.Local.sEmails.Declare(String)
V.Local.iC.Declare(Long)
F.Intrinsic.Debug.Stop
Gui.F_Contacts..Enabled(False)
F.Intrinsic.Control.CallSub(Set_econtacts_meth)

F.Intrinsic.String.Concat("DELETE FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='",V.Passed.000002,"'",V.Local.sQuery)
F.ODBC.Connection!conx.Execute(V.Local.sQuery)

F.Intrinsic.String.Split(V.Screen.F_Contacts!txtContacts.Text,",",V.Local.sEmails)
F.Intrinsic.Control.For(V.Local.iC,0,V.Local.sEmails.UBound,1)
	V.Local.sEmails(v.Local.iC).Set(V.Local.sEmails(v.Local.iC).Trim)
F.Intrinsic.Control.Next(V.Local.iC)
F.Intrinsic.String.RemoveArrayDuplicates(V.Local.sEmails,V.Local.sEmails)

F.Intrinsic.String.Concat("SELECT * FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='",V.Passed.000002,"'",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRW("rstContacts",V.Local.sQuery)
F.Intrinsic.Control.For(V.Local.iC,0,V.Local.sEmails.UBound,1)
	F.ODBC.conx!rstContacts.AddNew
	F.ODBC.conx!rstContacts.Set!CUSTOMER(V.Passed.000002)
	F.ODBC.conx!rstContacts.Set!CUST_EMAIL(V.Local.sEmails(v.Local.iC))
	F.ODBC.conx!rstContacts.Set!SALES_EMAIL(False)
	F.ODBC.conx!rstContacts.Update
F.Intrinsic.Control.Next(V.Local.iC)
F.ODBC.conx!rstContacts.Close

F.Intrinsic.Control.CallSub(Unload)

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("cmdsave_click_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_4640_Invoice_Delivery_VAR1.g2u",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.Debug.SetLA(V.Local.sError)
Function.Intrinsic.Control.EndIf

program.sub.cmdsave_click.End

Program.Sub.Check_eContacts_Meth.Start
'Checks to see how e-Invoicing contacts were added.  iType=0 if added from ATG_AUTO_EMAIL_CUST.gas from CRM, iType=1 if using script button from Customer Master (ATG_Invoice_Delivery.gas)
V.Local.sQuery.Declare(String)
V.Local.iType.Declare(Long)

V.Local.sQuery.Set("SELECT * FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='-1'")
F.ODBC.Connection!conx.OpenRecordsetRO("rstCheck",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstCheck.EOF,=,True)
	V.Local.iType.Set(0)
F.Intrinsic.Control.Else
	V.Local.iType.Set(1)
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstCheck.Close

F.Intrinsic.Variable.AddRV("iType",V.Local.iType)

Program.Sub.Check_eContacts_Meth.End

Program.Sub.Unload.Start
F.ODBC.Connection!conx.Close
F.Intrinsic.Control.End

Program.Sub.Unload.End

Program.Sub.Set_eContacts_Meth.Start
'Sets e-Invoicing contacts method as iType=1 (See Check_eContacts_Meth sub).  iType=0 if added from ATG_AUTO_EMAIL_CUST.gas from CRM, iType=1 if using script button from Customer Master (ATG_Invoice_Delivery.gas)
V.Local.sQuery.Declare(String)

V.Local.sQuery.Set("SELECT * FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='-1'")
F.ODBC.Connection!conx.OpenRecordsetRW("rstCheck",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstCheck.EOF,=,True)
	F.ODBC.conx!rstCheck.AddNew
	F.ODBC.conx!rstCheck.Set!Customer("-1")
	F.ODBC.conx!rstCheck.Set!Cust_email("-1")
	F.ODBC.conx!rstCheck.Set!Sales_email(False)
	F.ODBC.conx!rstCheck.Update
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstCheck.Close

Program.Sub.Set_eContacts_Meth.End

Program.Sub.ProcessInv.Start
F.Intrinsic.Control.SetErrorHandler("ProcessInv_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sParamName.Declare(String)
V.Local.sParamVal.Declare(String)
V.Local.sTemp.Declare(String)
V.Local.sPN.Declare(String)
V.Local.sPV.Declare(String)
V.Local.sCallParams.Declare(String)
V.Local.sAttach.Declare(String)
V.Local.sVersion.Declare(String)
V.Local.iRptID.Declare(Long)
V.Local.sPath.Declare(String)
V.Local.bExists.Declare(Boolean)
V.Local.bReportFound.Declare(Boolean)
V.Local.sQuery.Declare(String)
V.Local.sReport.Declare(String)
V.Local.bFile.Declare(Boolean)
'create variables for our new stuff
V.Local.sRequiredParams.Declare(String)
V.Local.sRetFile.Declare(String)
'required report params
F.Intrinsic.Control.If(V.Global.bParamCheck,=,False)
	V.Local.sRequiredParams.Set("TERMINAL*!*REPORTID*!*INVOICE")
	F.Intrinsic.String.Build("{0}\E_INVOICE_{1}",V.System.Temp,V.Caller.Terminal,V.Local.sRetFile)
F.Intrinsic.Control.EndIf

'Set attachment filename and path
F.Intrinsic.String.Concat(V.Caller.PluginsDir,"\Invoices\",V.Global.sInvoiceNo,"-",V.Global.sOrderNo,".pdf",V.Local.sAttach)

'Get GSS version for parameters
F.Intrinsic.String.Left(V.Caller.GSSVersion,4,V.Local.sVersion)

'Call GS program to build BI table data
F.Intrinsic.String.Concat(V.Global.sInvoiceNo,"!*!|~|CRYSTAL-OVERRIDE|~|",V.Local.sCallParams)
F.Intrinsic.Debug.SetLA("Generating Invoice BI data.  Using Callwrapper 200900: ",V.Local.sCallParams)
'F.Intrinsic.Debug.CallWrapperDebugEnable
F.Global.General.CallWrapperSyncBIO(200900,V.Local.sCallParams)
'F.Intrinsic.Debug.CallWrapperDebugDIsable
'F.Global.General.CallWrapperSync(200900,V.global.sInvoice)
'F.Intrinsic.Debug.Stop
'Call BI Order Acknowledgement report
F.Intrinsic.Control.CallSub(Getrptid)

V.Local.bReportFound.Set(False)
F.ODBC.Connection!conx.TableExists("ATG_EINVOICE_CCC",V.Local.bExists)
F.Intrinsic.Control.If(V.Local.bExists,=,True)
	F.Intrinsic.String.Concat("SELECT * FROM ATG_EINVOICE_CCC WHERE COMPANY_CODE='",V.Caller.CompanyCode,"'",V.Local.sQuery)
	F.ODBC.Connection!conx.OpenRecordsetRO("rstReport",V.Local.sQuery)
	F.Intrinsic.Control.If(V.ODBC.conx!rstReport.EOF,<>,True)
		V.Local.bReportFound.Set(True)
		F.Intrinsic.String.Concat("E-INVOICE-",V.Caller.CompanyCode,V.Local.sReport)
		V.Local.sPath.Set(V.ODBC.conx!rstReport.FieldValTrim!REPORT_LOC)
		F.Intrinsic.File.Exists(V.Local.sPath,V.Local.bExists)
		F.Intrinsic.Control.If(V.Local.bExists,<>,True)
			F.Intrinsic.String.Right(V.Local.sPath,4,V.Local.sTemp)
			F.Intrinsic.Control.If(V.Local.sTemp.UCase,<>,".RPT")
				F.Intrinsic.String.Concat(V.Local.sPath,".rpt",V.Local.sPath)
				F.Intrinsic.File.Exists(V.Local.sPath,V.Local.bExists)
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.If(V.Local.bExists,=,True)
			F.Global.BI.InitializeReport(V.Local.sReport,V.Local.sPath,V.Local.iRptID)
			F.Intrinsic.Control.If(V.Local.iRptID,=,-1)
				F.Intrinsic.String.Concat("Unable to initialize a report for e-Invoicing in the following location setup for company code ",V.Caller.CompanyCode," in ATG_EINVOICE_CCC: ",V.Local.sPath,".  Refer to GAB Forum for more info.  Hotfix #222 may need to be applied.",V.Local.sPath)
				F.Intrinsic.UI.Msgbox(V.Local.sPath,"e-Invoicing")
				F.Intrinsic.UI.Msgbox("Please set up E-Invoice-CCC.rpt.  Refer to GAB Forum for more info.")
				F.Intrinsic.Control.CallSub(Unload)
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.Else
			F.Intrinsic.String.Concat("Unable to find e-Invoice in the following location setup for company code ",V.Caller.CompanyCode," in ATG_EINVOICE_CCC: ",V.Local.sPath,".  Refer to GAB Forum for more info",V.Local.sPath)
			F.Intrinsic.UI.Msgbox(V.Local.sPath,"e-Invoicing")
			F.Intrinsic.Control.CallSub(Unload)
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.EndIf
	F.ODBC.conx!rstReport.Close
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.If(V.Local.bReportFound,=,False)
	F.Global.BI.GetIDFromName("E-INVOICE.rpt",V.Local.iRptID)
	F.Intrinsic.Control.If(V.Local.iRptID,=,-1)
		F.Intrinsic.String.Concat(V.Caller.BusintDir,"\Custom\E-INVOICE.RPT",V.Local.sPath)
		F.Intrinsic.File.Exists(V.Local.sPath,V.Local.bExists)
		F.Intrinsic.Control.If(V.Local.bExists,=,False)
			F.Intrinsic.UI.Msgbox("Please set up E-Invoice.rpt.  Refer to GAB Forum for more info.")
			F.Intrinsic.Control.CallSub(Unload)
		F.Intrinsic.Control.EndIf
		F.Global.BI.InitializeReport("E-INVOICE.rpt",V.Local.sPath,V.Local.iRptID)
		F.Intrinsic.Control.If(V.Local.iRptID,=,-1)
			F.Intrinsic.UI.Msgbox("Please set up E-Invoice.rpt.  Refer to GAB Forum for more info.")
			F.Intrinsic.Control.CallSub(Unload)
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.CallSub("Check_Table_Data","RptID",V.Args.RptID,"CustID",V.Local.iRptID)
F.Intrinsic.Control.If(V.Args.bDataExists,=,False)
	F.Intrinsic.Control.CallSub(Unload)
F.Intrinsic.Control.EndIf

'getting run id and log id
'mod by trt.
F.Global.BI.GetRunID(V.Global.iBIRUNID)
F.Global.BI.StartLogging(V.Global.iBIRUNID,V.local.iRptID,-1,"E-Invoice.rpt",V.Global.iLOGID)

f.Intrinsic.Control.If(V.Caller.GSSVersion.float,>=,2011.1)
	V.Local.sPN.Set("REPORTID*!*TERMINAL*!*ORDERNO*!*PROGRAM*!*TRACK*!*IVCTYPE*!*PRTSHIP*!*PRTORD*!*INVOICE")
	F.Intrinsic.String.Concat(V.args.RptID,"*!*",V.caller.Terminal,"*!*",V.Global.sOrderNo,"*!*",V.Caller.Caller,"*!**!**!**!**!*",V.Global.sInvoiceNo,V.Local.sPV)
F.Intrinsic.Control.Else
	V.Local.sPN.Set("REPORTID*!*TERMINAL*!*INVOICE*!*COMMIVC")
	F.Intrinsic.String.Concat(V.args.RptID,"*!*",V.caller.Terminal,"*!*",V.Global.sInvoiceNo,"*!* ",V.Local.sPV)
F.Intrinsic.Control.EndIf

'F.Intrinsic.String.Split(V.Local.sPN,"*!*",V.Local.sParamName)
'F.Intrinsic.String.Split(V.Local.sPV,"*!*",V.Local.sParamVal)
'F.Global.BI.SaveReport(000055,1,V.Local.sParamName,V.Local.sParamVal,V.Local.sAttach)
'F.Global.BI.SaveReport(V.Local.iRptID,1,V.Local.sParamName,V.Local.sParamVal,V.Local.sAttach)
'replace the print report with run report from preprocessor. comment out the splitting you dont need that.
F.Global.BI.StopLogging(V.Global.iLOGID)
F.Intrinsic.Control.If(V.Global.bParamCheck,=,False)
	F.Global.BI.RunReportPreProcessor(V.Global.iBIRUNID,V.Global.iLOGID,V.Local.sPN,V.Local.sPV,"E-Invoice.rpt",4,True,"",-1,"",0,V.Local.sAttach,"",V.Local.sRequiredParams,V.Local.sRetFile,V.Global.iLOGID)
F.Intrinsic.Control.Else
	F.Global.BI.RunReportPreProcessor(V.Global.iBIRUNID,V.Global.iLOGID,V.Local.sPN,V.Local.sPV,"E-Invoice.rpt",4,True,"",-1,"",0,V.Local.sAttach,"",V.Global.iLOGID)
F.Intrinsic.Control.EndIf

F.Intrinsic.File.Exists(V.Local.sAttach,V.Local.bFile)
F.Intrinsic.Control.If(V.Local.bFile,=,False)
	F.Intrinsic.UI.Msgbox("Invoice PDF was not created. Please contact Global Shop.")
	V.Passed.Global.Set("False")
	F.Intrinsic.Control.CallSub(Unload)
F.Intrinsic.Control.Else
	F.Intrinsic.Variable.PassedExists("GLOBAL",V.Local.bFile)
	F.Intrinsic.Control.If(V.Local.bFile,=,True)
		V.Passed.global.Set("True")
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf
F.Intrinsic.Variable.AddRV("sAttach",V.Local.sAttach)

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("ProcessInv_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_4640_Invoice_Delivery_VAR1.g2u",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.Debug.SetLA(V.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.ProcessInv.End

Program.Sub.GetRptID.Start
F.Intrinsic.Control.SetErrorHandler("GetRptID_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sQuery.Declare(String)
V.Local.sReportID.Declare(String)


'bug call originall from enoch. TRT
'dont grab the rpt id where invoice number could be 000113 commercial invoice. this then carries over to check table data. It's looking for the old persons terminal which may/may not be the same person or terminal.
F.Intrinsic.String.concat("SELECT * FROM V_PRT_LASER_INVOICE WHERE INVOICE_NO='",V.Global.sInvoiceNo,"' And RPTID <> '000113' ",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRO("rstRptID",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstRptID.EOF,<>,True)
	V.local.sReportID.set(V.odbc.conx!rstRptID.fieldvaltrim!RPTID)
F.Intrinsic.Control.Else
	F.Intrinsic.UI.Msgbox("Unable to find Invoice Data.  Please contact Global Shop.")
	F.Intrinsic.Control.CallSub(Unload)
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstRptID.Close

F.Intrinsic.Variable.AddRV("RptID",V.Local.sReportID)

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("GetRptID_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_4640_Invoice_Delivery_VAR1.g2u",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.Debug.SetLA(V.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.GetRptID.End

Program.Sub.Check_Table_Data.Start
V.Local.sQuery.Declare(String)
V.Local.sError.Declare(String)
V.Local.bExists.Declare(Boolean)

F.Intrinsic.Control.SetErrorHandler("Sub_Err")
F.Intrinsic.Control.ClearErrors

F.Intrinsic.Debug.SetLA("Checking Invoice Table Data.  Rpt ID=",V.Args.RptID," Terminal=",V.Caller.Terminal," Invoice No= ",V.Global.sInvoiceNo," Custom RptID=",V.Args.CustID)

F.Intrinsic.String.Concat("SELECT * FROM PRT_LASER_INVOICE WHERE TERMINAL_NO='",V.Caller.Terminal,"' AND RPTID='",V.Args.RptID,"' AND INVOICE_NO='",V.Global.sInvoiceNo,"'",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRO("rstCheck",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstCheck.EOF,=,True)
	F.ODBC.conx!rstCheck.Close
	F.Intrinsic.String.Concat("SELECT * FROM PRT_LASER_INVOICE WHERE RPTID='",V.Args.RptID,"' AND INVOICE_NO='",V.Global.sInvoiceNo,"'",V.Local.sQuery)
	F.ODBC.Connection!conx.OpenRecordsetRO("rstCheck",V.Local.sQuery)
	F.Intrinsic.Control.If(V.ODBC.conx!rstCheck.EOF,=,True)
		F.ODBC.conx!rstCheck.Close
		F.Intrinsic.String.Concat("SELECT * FROM PRT_LASER_INVOICE WHERE INVOICE_NO='",V.Global.sInvoiceNo,"'",V.Local.sQuery)
		F.ODBC.Connection!conx.OpenRecordsetRO("rstCheck",V.Local.sQuery)
		F.Intrinsic.Control.If(V.ODBC.conx!rstCheck.EOF,=,True)
'			F.ODBC.conx!rstCheck.Close
			F.Intrinsic.Debug.SetLA("There has been an issue when generating e-Invoice.  Invoice #",V.Global.sInvoiceNo," not found.  Please contact Global Shop.")
			F.Intrinsic.UI.Msgbox(V.Ambient.LastAction)
		F.Intrinsic.Control.Else
			F.Intrinsic.Debug.SetLA("There has been an issue when generating e-Invoice #",V.Global.sInvoiceNo,".  Terminal ID and Report ID is invalid.  Please contact Global Shop.",V.Ambient.NewLine,V.Ambient.NewLine,"Data exists,but terminal and report ID invalid. Terminal in table: ",V.ODBC.conx!rstCheck.FieldValTrim!TERMINAL_NO," rpt ID in table:",V.ODBC.conx!rstCheck.FieldValTrim!RPTID,".Query:",V.Local.sQuery)
			F.Intrinsic.UI.Msgbox(V.Ambient.LastAction)
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.Else
		F.Intrinsic.Debug.SetLA("There has been an issue when generating e-Invoice #",V.Global.sInvoiceNo,".  Terminal ID is invalid.  Please contact Global Shop.",V.Ambient.LastAction,V.Ambient.LastAction,"Data exists,but terminal invalid. Terminal in table: ",V.ODBC.conx!rstCheck.FieldValTrim!TERMINAL_NO,". Query: ",V.Local.sQuery)
		F.Intrinsic.UI.Msgbox(V.Ambient.LastAction)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.Else
	F.Intrinsic.Debug.SetLA("Data exists: ",V.Local.sQuery)
	V.Local.bExists.Set(True)
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstCheck.Close

F.Intrinsic.Variable.AddRV("bDataExists",V.Local.bExists)

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Sub_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.Check_Table_Data.End

Program.Sub.PopDefaultText.Start
'Added 11-24-10 for USPLabs
V.Local.sFile.Declare(String)
V.Local.sText.Declare(String)
V.Local.bExists.Declare(Boolean)
V.Local.sSubject.Declare(String)
V.Local.sBody.Declare(String)
V.Local.sError.Declare(String)
V.Local.sDir.Declare(String)
V.Local.sDate.Declare(String)

Function.Intrinsic.Control.SetErrorHandler("PopDefaultText_Err")
Function.Intrinsic.Control.ClearErrors

F.Intrinsic.Control.CallSub(Getcustinfo)

V.Local.sSubject.Set("Attached are your Invoices(s)")
v.Local.sText.Set("Attached are your Invoices(s). Thank you.") 

F.Intrinsic.String.Build("{0}\4640_E-Invoice_Subject.txt",V.Caller.FilesDir,V.Local.sFile)
F.Intrinsic.File.Exists(V.Local.sFile,V.Local.bExists)
f.Intrinsic.Control.If(v.Local.bExists,=,True) 
	f.Intrinsic.File.File2String(v.Local.sFile,v.Local.sSubject)
	F.Intrinsic.String.Replace(V.Local.sSubject,"{{CUSTOMER}}",V.Global.sCustomerName,V.Local.sSubject)
F.Intrinsic.Control.Else
	V.Local.sSubject.Set("Attached are your Invoices(s)")
Function.Intrinsic.Control.EndIf

F.Intrinsic.String.Build("{0}\Plugins\GAB\GAS",v.Caller.GlobalDir,v.Local.sDir) 
F.Intrinsic.String.Build("{0}\EInvoicing_Body_Text_{1}.htm",v.Local.sDir,v.Caller.CompanyCode,v.Local.sFile) 
F.Intrinsic.File.Exists(v.Local.sFile,v.Local.bExists) 
f.Intrinsic.Control.If(v.Local.bExists,=,True) 
	f.Intrinsic.File.File2String(v.Local.sFile,v.Local.sText)
Function.Intrinsic.Control.Else
'	look for the default
	f.Intrinsic.String.Build("{0}\EInvoicing_Body_Text_ccc.htm",v.Local.sDir,v.Local.sFile) 
	f.Intrinsic.File.Exists(v.Local.sFile,v.Local.bExists) 
	f.Intrinsic.Control.If(v.Local.bExists,=,True) 
		f.Intrinsic.File.File2String(v.Local.sFile,v.Local.sText) 
	Function.Intrinsic.Control.Else 
'		if that doesn't exist use default. 
		v.Local.sText.Set("Attached are your Invoices(s). Thank you.") 
	Function.Intrinsic.Control.EndIf	
Function.Intrinsic.Control.EndIf

'F.Intrinsic.File.File2String(V.Local.sFile,V.Local.sText)
F.Intrinsic.String.Replace(V.Local.sText,"{{CUSTOMER}}",V.Global.sCustomerName,V.Local.sText)
F.Intrinsic.String.Replace(V.Local.sText,"{{COMPANY}}",V.Ambient.CompanyName,V.Local.sText)
F.Intrinsic.String.Format(V.Ambient.Date,"mm/dd/yyyy",V.Local.sDate)
F.Intrinsic.String.Replace(V.Local.sText,"{{DATE}}",V.Local.sDate,V.Local.sText)
F.Intrinsic.String.Split(V.Local.sText,"*!*",V.Local.sText)

V.Local.sBody.Set(V.Local.sText)

F.Intrinsic.Variable.AddRV("Body",V.Local.sBody)
F.Intrinsic.Variable.AddRV("Subject",V.Local.sSubject)

Function.Intrinsic.Control.ExitSub
Function.Intrinsic.Control.Label("PopDefaultText_Err")
Function.Intrinsic.Control.If(Variable.Ambient.ErrorNumber,"<>",0)
	Function.Intrinsic.String.Concat("Error Occurred ",Variable.Ambient.ErrorNumber," with description ",Variable.Ambient.ErrorDescription,Variable.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.PopDefaultText.End

Program.Sub.GetCustInfo.Start
'Added 11/24/10 for USPLabs
V.Local.sQuery.Declare(String)
V.Local.sCustPO.Declare(String)
V.Local.sError.Declare(String)

Function.Intrinsic.Control.SetErrorHandler("GetCustPO_Err")
Function.Intrinsic.Control.ClearErrors

'F.Intrinsic.String.Concat("SELECT CUSTOMER_PO FROM V_ORDER_HEADER WHERE ORDER_NO='",V.Global.sInvoiceNo,"'",V.Local.sQuery)
F.Intrinsic.String.Concat("SELECT CUSTOMER_PO FROM V_ORDER_HIST_HEAD WHERE Invoice='",V.Global.sInvoiceNo,"'",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRO("rstCustPO",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstCustPO.EOF,<>,True)
	V.Global.sCustPO.Set(V.ODBC.conx!rstCustPO.FieldValTrim!CUSTOMER_PO)
F.Intrinsic.Control.Else
	F.ODBC.conx!rstCustPO.Close
	F.Intrinsic.String.LPad(V.Global.sOrderNo,"0",7,V.Local.sQuery)
	F.Intrinsic.String.Concat("SELECT CUSTOMER_PO FROM V_ORDER_HEADER WHERE ORDER_NO='",V.Local.sQuery,"'",V.Local.sQuery)
	F.ODBC.Connection!conx.OpenRecordsetRO("rstCustPO",V.Local.sQuery)
	F.Intrinsic.Control.If(V.ODBC.conx!rstCustPO.EOF,<>,True)
		V.Global.sCustPO.Set(V.ODBC.conx!rstCustPO.FieldValTrim!CUSTOMER_PO)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstCustPO.Close

F.Intrinsic.String.Concat("SELECT NAME_CUSTOMER FROM V_CUSTOMER_MASTER WHERE CUSTOMER='",V.Global.sCustomer,"'",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRO("rstCustPO",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstCust.EOF,<>,True)
	V.Global.sCustomerName.Set(V.ODBC.conx!rstCustPO.FieldValTrim!NAME_CUSTOMER)
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstCustPO.Close

Function.Intrinsic.Control.ExitSub
Function.Intrinsic.Control.Label("GetCustPO_Err")
Function.Intrinsic.Control.If(Variable.Ambient.ErrorNumber,"<>",0)
	Function.Intrinsic.String.Concat("Error Occurred ",Variable.Ambient.ErrorNumber," with description ",Variable.Ambient.ErrorDescription,Variable.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.GetCustInfo.End

Program.Sub.Comments.Start
${$0$}$$}$$}$12:00:00 AM$}$False
Program.Sub.Comments.End

